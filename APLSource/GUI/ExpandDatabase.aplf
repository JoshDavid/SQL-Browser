 ExpandDatabase←{
⍝ Connect to the database, and pull in Schemas
⍝ ⍵ ←→ (tree obj)(config obj)
⍝ ⍺ ←→ item, which is the connection name
     t c←⍵
     s←c t ##.DB.Connect ⍺
     s≡¯1:''
     oldCursor←t SetCursor'Wait'
     schemas tables←↓⍉##.DB.GetTables ⍺
     dMat←schemas{(1 2⍴⍺,1)(⍵,(⍤¯1)2)}⌸tables
     items←t.Items
     depths←t.Depth
     start←items⍳⊆⍺
     cItems←⊃,/{⍵[;1]}¨,dMat
     cDepths←∊{⍵[;2]}¨,dMat
     t.(Items Depth)←cItems cDepths{(start↑⍵),⍺,(start↓⍵)}¨items depths
     t SetCursor oldCursor
 }
